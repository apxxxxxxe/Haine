//******************************************************************************
// YAYA　ゴーストテンプレート
// マウス入力関連辞書
//******************************************************************************


//******************************************************************************
//マウス処理部
//
//以下、実際のトークを記述します。関数名は以下の規則になっています。
//[種別] + [スコープ] + [部位]
//種別は、MouseMove, MouseDoubleClick, MouseWheelUp, MouseWheelDown のどれかです。
//スコープは、\0,\1,といったスコープ番号です。
//部位は、当たり判定のどの部位かを表します。当たり判定の無い場所でのイベントの場合
//部位はありません。
//******************************************************************************


//******************************************************************************
//撫で反応
//******************************************************************************

MouseMove0head : nonoverlap
{
  if touch_count == 1 && ghostuptime < 60 {
    "h111109あっ……h111209ふふ…………。\nh111204どうしたのよ、いきなり。"
  }else {
    "h111209ん……ありがとう。\nh111205あなたも無理しないでね。"
    "h111209あたたかいのね、あなたの手。"
    "h111209……誰かになでられる機会なんて、\nどんどんなくなってしまうものよね。\n…………h111205うん、ありがとう。\nh114201さ、いらっしゃい。あなたの頭もなでてあげるわh114207。"
  }
}

MouseMove0face : nonoverlap
{
  if touch_count == 1 && ghostuptime < 60 {
    "h111408ひゃっ！\n……h324207冷たくて驚いちゃった。外はずいぶん寒いのね。\nh116201風邪をひくといけないわね。\nh100000暖炉のそばにいらっしゃい。体をあたためましょう。h114207"
  }else {
    "h111205やさしく触れてくれるのね。\nh111209嬉しいわ、ありがとう。"
    "h111207くすぐったいわよ。"
  }
}

MouseMove0bust : nonoverlap
{
  if touch_count == 1 && ghostuptime < 60 {
    "h321209……会って早々することがこれなの？\nh321204どうしようもない人ね、ユーザ。"
  } elseif touch_count == 1 && RAND(2) == 0 {
    "h322104ちょ、ちょっと……\nh322209ふざけすぎよ、ユーザ。"
  }else {
    "h124206……気が済んだらやめてね。"
    "h324209……なんというか……困ってしまうわ。"
  }
}

//******************************************************************************
//つつかれ反応
//******************************************************************************

MouseDoubleClick0
{
  OnOpenMenu
}

//******************************************************************************
// 引き寄せ反応（下ホイール）
//******************************************************************************

MouseWheelDown0shoulder
{
  "h114209ん。\nh114205……。\nh114201………h114204ふふ。"
}

MouseWheelDown0hand
{
  "h114202うん？\n……h114205手を引いて、h114204それからどうするの？"
  "h114204……どこかへ連れて行ってくれるの？"
}

//******************************************************************************
// 見つめ反応（静止時）
//******************************************************************************

OnMouseLookAtbust : nonoverlap
{
  '……h112204視線って案外わかるのよ？\nやるならh112207悟られないようにしないと。'
  '……h212109見すぎよ。'
}

OnMouseLookAtBeforebust : nonoverlap
{
  "h112104"
}

OnMouseLookAtface : nonoverlap
{
  '……h324207顔に何かついてる？'
  'h321201……h321207そんなに見つめられると、困ってしまうわ。\nh121204面白いわけでもないでしょう？'
}

OnMouseLookAtBeforeface : nonoverlap
{
  "h324201"
}

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************

// 触り反応時の共通処理
CommonEventMousePre {
  lastresponce = systemuptime
}

CommonEventMousePost {
  first_touch = 1
}

TouchCount {
  // _argv[0]: 触り反応の部位
  if last_touch_part == _argv[0] {
    touch_count = touch_count + 1
  } else {
    touch_count = 1
  }
  last_touch_part = _argv[0]
}

OnMouseMove
{
  _result = AYATEMPLATE.MouseMoveWheelFunc(0,50,'MouseMove')
  if _result != '' {
    TalkStatus = 'talking'
    CommonEventMousePre
    TouchCount(reference[4])
    CanselLookAt + _result + "\![raise,CommonEventMousePost]"
  }
}

OnMouseWheel
{
  _result = ''
  if TOINT(reference[2]) > 0 {
    _result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelUp')
  }
  else {
    _result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelDown')
  }
  
  if _result == '' {
    _result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheel')
  }
  
  if _result != '' {
    TalkStatus = 'talking'
    CommonEventMousePre
    TouchCount(reference[4])
    CanselLookAt + _result + "\![raise,CommonEventMousePost]"
  }
}

OnMouseDoubleClick
{
  _result = AYATEMPLATE.MouseEventExec('MouseDoubleClick')
  if _result != '' {
    TalkStatus = 'talking'
    CommonEventMousePre
    TouchCount(reference[4])
    CanselLookAt + _result + "\![raise,CommonEventMousePost]"
  }
}

OnMouseClickEx
{
  if reference[5] == 'middle' {
    _result = AYATEMPLATE.MouseEventExec('MouseWheelClick')
    if _result != '' {
      if TalkStatus != 'menu' {TalkStatus = 'talking'}
      CommonEventMousePre
      TouchCount(reference[4])
      CanselLookAt + _result + "\![raise,CommonEventMousePost]"
    }
  }
}

OnMouseHover
{
  "\C\![timerraise,1000,1,OnMouseLookAtBefore,%(reference[4])]\![timerraise,2000,1,OnMouseLookAt%(reference[4])]"
}

OnMouseLeave
{
  "\C" + CanselLookAt(reference[4])
}

CanselLookAt {
  "\![timerraise,0,1,OnMouseLookAtBefore%(_argv[0])]\![timerraise,0,1,OnMouseLookAt%(_argv[0])]"
}

OnMouseLookAtBefore {
  "\C\![raise,OnMouseLookAtBefore%(reference[0])]"
}

