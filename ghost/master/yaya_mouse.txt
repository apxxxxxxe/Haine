//******************************************************************************
// YAYA　ゴーストテンプレート
// マウス入力関連辞書
//******************************************************************************


//******************************************************************************
//マウス処理部
//
//以下、実際のトークを記述します。関数名は以下の規則になっています。
//[種別] + [スコープ] + [部位]
//種別は、MouseMove, MouseDoubleClick, MouseWheelUp, MouseWheelDown のどれかです。
//スコープは、\0,\1,といったスコープ番号です。
//部位は、当たり判定のどの部位かを表します。当たり判定の無い場所でのイベントの場合
//部位はありません。
//******************************************************************************


//******************************************************************************
//撫で反応
//******************************************************************************

MouseMove0head : nonoverlap
{
  touch_count_head++
  if continuous_touch_count == 1 {
    "h111209ふふ……いっぱいなでてくれるのね。\nh111205あなたの手、あたたかくて好きよ。"
  } elseif continuous_touch_count > 1 {
    "h111209……。"
    "h111209……。"
    "h111209……。"
    "h111209……。"
    "h111209……ふふ……。"
  } else {
  "h111209ん……ありがとう。\nh111205あなたも無理しないでね。"
  "h111209あたたかいのね、あなたの手。"
  "h111209……誰かになでられる機会なんて、\nどんどんなくなってしまうものよね。\n…………h111205うん、ありがとう。\nh111201さ、いらっしゃい。あなたの頭もなでてあげるわh111207。"
  }
}

MouseMove0face : nonoverlap
{
  "h111205やさしく触れてくれるのね。\nh111209嬉しいわ、ありがとう。"
  "h111207くすぐったいわよ。"
}

MouseMove0bust : nonoverlap
{
  touch_count_bust++
  if continuous_touch_count == 0 {
    "h312408%(DialogSurprised)h312401\_w[700]\nh322209ふざけすぎよ、ユーザ。"
  } elseif continuous_touch_count == 1 {
    "h121206……気が済んだらやめてね。"
    "h321209……なんというか……困ってしまうわ。"
  } else {
    "h121109……。"
    "h121109……。"
    "h121109……。"
    "h121109……。"
    "h111104……はあ……。"
  }
}

//******************************************************************************
//つつかれ反応
//******************************************************************************

MouseDoubleClick0
{
  if continuous_touch_count == 1 {
    OnOpenMenu("h321201%(DialogConfused)聞こえてるわよ？")
  } elseif continuous_touch_count > 1 {
    OnOpenMenu("h321209……。")
  } else {
    OnOpenMenu
  }
}

MouseDoubleClick0bust
{
  touch_count_bust++
  OnOpenMenu("h312109……普通に呼んで。")
}

//******************************************************************************
// 引き寄せ反応（下ホイール）
//******************************************************************************

MouseWheelDown0shoulder
{
  if continuous_touch_count == 1 {
    "h100000あっ……。h111209…………h111205まだ、こうしていたいの？"
  } elseif continuous_touch_count > 1 {
    "h111209……。"
  } else {
    "h111209ん。\nh111205……。\nh111201………h111204ふふ。"
  }
}

MouseWheelDown0hand
{
  if continuous_touch_count == 1 {
    "h111205ぎゅーっ……h111201。h111204ふふ。"
    "h111209……離してくれないの？困ったわね……h111207ふふ……"
  } elseif continuous_touch_count > 1 {
    "……。"
  } else {
    "h111202うん？\n……h111205手を引いて、h111204それからどうするの？"
    "h111204……どこかへ連れて行ってくれるの？"
  }
}

//******************************************************************************
// 見つめ反応（静止時）
//******************************************************************************

OnMouseLookAtbust : nonoverlap
{
  touch_count_bust++
  '……h112204視線って案外わかるのよ？\nやるならh112207悟られないようにしないと。'
  '……h212109見すぎよ。'
}

OnMouseLookAtBeforebust : nonoverlap
{
  "h112104"
}

OnMouseLookAtface : nonoverlap
{
  '……h321207顔に何かついてる？'
  'h321201……h321207そんなに見つめられると、困ってしまうわ。\nh121204面白いわけでもないでしょう？'
}

OnMouseLookAtBeforeface : nonoverlap
{
  "h321201"
}

DialogSurprised {
  '！……'
  'なっ……'
}

DialogConfused {
  '……'
  'あの……'
}

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************

// 触り反応時の共通処理
CommonEventMousePre {
  TalkStatus = 'touch'
  lastresponce = systemuptime
  first_touch = 1
}

OnMouseEvent {
  InsertLineFeed(SetBalloonSurface + CanselLookAt + NextTouchTalk)
}

CommonEventMousePost {
}

TouchCount {
  // _argv[0]: 触り反応の部位
  if last_touch_part == _argv[0] {
    continuous_touch_count++
  } else {
    continuous_touch_count = 0
  }
  last_touch_part = _argv[0]
  "\![timerraise,30000,1,OnResetTouchCount]"
}

OnResetTouchCount {
  last_touch_part = ""
  continuous_touch_count = 0
}

OnMouseMove
{
  NextTouchTalk = AYATEMPLATE.MouseMoveWheelFunc(0,50,'MouseMove')
  if NextTouchTalk != '' {
    TouchCount(reference[4])
    --
    CommonEventMousePre
    '\![raise,OnMouseEvent]'
  }
}

OnMouseWheel
{
  NextTouchTalk = ''
  if TOINT(reference[2]) > 0 {
    NextTouchTalk = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelUp')
  }
  else {
    NextTouchTalk = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelDown')
  }

  if NextTouchTalk == '' {
    NextTouchTalk = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheel')
  }

  if NextTouchTalk != '' {
    TouchCount(reference[4])
    --
    CommonEventMousePre
    '\![raise,OnMouseEvent]'
  }
}

OnMouseDoubleClick
{
  NextTouchTalk = AYATEMPLATE.MouseEventExec('MouseDoubleClick')
  if NextTouchTalk != '' {
    TouchCount(reference[4])
    --
    CommonEventMousePre
    '\![raise,OnMouseEvent]'
  }
}

OnMouseClickEx
{
  if reference[5] == 'middle' {
    NextTouchTalk = AYATEMPLATE.MouseEventExec('MouseWheelClick')
    if NextTouchTalk != '' {
      TouchCount(reference[4])
      --
      CommonEventMousePre
      '\![raise,OnMouseEvent]'
    }
  }
}

CanselLookAt {
  "\![timerraise,0,1,OnMouseLookAtBefore%(_argv[0])]"
  --
  "\![timerraise,0,1,OnMouseLookAt%(_argv[0])]"
}

OnMouseLookAtBefore {
  "\C\![raise,OnMouseLookAtBefore%(reference[0])]"
}

