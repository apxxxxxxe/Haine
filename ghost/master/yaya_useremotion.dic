ShowUserEmotions
{
  _fontsize = 12

  _labels = IARRAY
  _emo_addends = IARRAY
  for _i=0; _i<ARRAYSIZE(_argv)/2; _i++ {
    _labels ,= _argv[_i * 2]
    _emo_addends ,= _argv[_i * 2 + 1]
  }

  _emotions_strlen = 0
  _emotions = IARRAY
  for _i=0; _i<ARRAYSIZE(emo_labels); _i++ {
    _e = "\f[height,12]%(emo_labels[_i])"
    _isnotchanged = 1
    for _j=0; _j<ARRAYSIZE(_labels); _j++ {
      if emo_labels[_i] == _labels[_j] {
        _isnotchanged = 0
        _e += (user_emotions[_i] + _emo_addends[_j])
        if _emo_addends[_j] >= 0 {
          _e += '\f[color,0,250,0]+'
        }
        else {
          _e += '\f[color,250,0,0]'
        }
        _e += "%(_emo_addends[_j])\f[color,default]"
      }
    }
    if _isnotchanged {
      _e += user_emotions[_i]
    }

    _emotions ,= "%(_e)\f[height,default]"
    _emotions_strlen += STRLENHALF(SHIORI3FW.RemoveAllTags(_e))
  }

  _x_limit = ARRAYSIZE(_emotions)
  _y_limit = 1
  _perline_strlen = _emotions_strlen / _x_limit
  while _fontsize * _perline_strlen * _x_limit > balloow_width {
    _x_limit /= 2
    _y_limit *= 2
  }

  _emotions_script = '  '
  for _i=0; _i<ARRAYSIZE(emo_labels); _i++ {
    _l = TOINT(STRLENHALF(SHIORI3FW.RemoveAllTags(_emotions[_i])))
    _x = _i % _x_limit
    _y = _i / _x_limit
    _emotions_script += "\_l[%(balloow_width/(_x_limit+1)*(_x+1)),%(balloon_rows_1-_y_limit+_y+1)em]\_l[@-%(_l*12/2),]%(_emotions[_i])"
  }

  _emotions_script = ERASE(_emotions_script, 0, STRSTR(_emotions_script, '\', 2))

  "\1\_q%(_emotions_script)\n\_q\_l[0,0]"
  --
  if ARRAYSIZE(_argv) {
    _references = ''
    for _i=0; _i<ARRAYSIZE(_argv); _i++ {
      _references += "%(_argv[_i]),"
    }
    _references = ERASE(_references, -1, 1)
    "\![embed,OnUpdateUserEmotions,%(_references)]"
  }
}

AddUserEmotions
{
  "\1\c[line,%(ARRAYSIZE(emo_labels)),0]" + ShowUserEmotions(_argv)
}

OnUpdateUserEmotions
{
  for _i=0; _i<ARRAYSIZE(reference)/2; _i++ {
    for _j=0; _j<ARRAYSIZE(emo_labels); _j++ {
      if emo_labels[_j] == reference[_i * 2] {
        user_emotions[_j] += reference[_i * 2 + 1]
        if user_emotions[_j] < 1 {
          user_emotions[_j] = 1
        }
      }
    }
  }
}
