
//--------------------------------------------------
// ポモドーロタイマー
//--------------------------------------------------

UpdatePomodoroTimer
{
  if PomodoroStatus > 0 // ポモドーロタイマーが作動中なら
  {
    TimerSec = systemuptime - base_time // タイマー開始からの秒数
    TimerMin = TimerSec / 60 // タイマー開始からの分数
    if TimerSec >= (currentpomodoro * 60)
    {
      if TalkStatus == 'menu'
      {
        WillResetPomodoro = 1
      }
      else
      {
        ResetPomodoroTimer(currentpomodoro) // 時間になったらタイムアップ時の処理
      }
    }
  }
}

OnPomodoroSet
{
  pomodoro = (reference[0], reference[1])
  ResetPomodoroTimer(-1)
}

ResetPomodoroTimer
{
  /*
     _argv[0]: -1;開始 pomodoro[0];休憩開始 pomodoro[1];作業再開
     */
  _ret = "\_v[sound/柱時計の鐘.mp3]"
  if _argv[0] == -1 // 開始時
  {
    PomodoroStatus = 1
    PomodoroCount = 0
    currentpomodoro = pomodoro[0]
    _ret += "h111204ポモドーロタイマーを開始するわ。\nh111209まずは%(currentpomodoro)分ね。"
  }
  elseif (_argv[0] == pomodoro[0]) // 作業時間が終わったとき
  {
    currentpomodoro = pomodoro[1]
    _ret += "h111207お疲れ様。%(_argv[0])分が経過したわ。\nh111204これから%(currentpomodoro)分の休憩ね。"
  }
  elseif (_argv[0] == pomodoro[1]) // 休憩時間が終わったとき
  {
    currentpomodoro = pomodoro[0]
    PomodoroCount += 1
    _ret += "h111207%(_argv[0])分が経過したわ。\nh111204続けて%(currentpomodoro)分の作業、開始するわね。"
    _ret += "\nちなみに現在%(PomodoroCount)セット（%(EVAL((pomodoro[0]+pomodoro[1])*PomodoroCount))分）よ。"
  }

  base_time = systemuptime // 経過時間の基準点を再設定
  _ret
}

OnPomodoroStop
{
  if (PomodoroCount == 0) {
    "h111209はい、中断したわ。"
  }else {
    "h111207お疲れさま。\n今回は" + PomodoroCount + "セット（" + (pomodoro[0]+pomodoro[1])*PomodoroCount + "分）ね。"
  }
  PomodoroStatus = 0
  AllPomodoroCount += PomodoroCount
}

//--------------------------------------------------
// ループタイマー
//--------------------------------------------------
OnLoopSet
{
  PomodoroStatus = 2
  CurrentLoop = reference[0]
  "\![raise,OnLoopTimerOver,100]"
}

OnLoopTimerOver
{
  /*
     reference[0]:
     */
  TimerMin = 0
  _loop = CurrentLoop * 1000 * 60
  "\![timerraise,%(_loop),1,OnLoopTimerOver,0]\_v[sound/柱時計の鐘.mp3]"
  --

  if (reference[0] == 100) {
    "%(CurrentLoop)分のループタイマーを開始するわ。"
    LoopCount = 0
  } else {
    "%(CurrentLoop)分が経過したわ。次は%(CurrentLoop)分後ね。"
    LoopCount += 1
  }
}

OnTimerStop
{
  if (reference[0] == 0) {
    "h111209はい、中断したわ。"
  }else {
    "h111207お疲れさま。\n今回は" + CurrentLoop + "セット（" + CurrentLoop*LoopCount + "分）ね。"
  }
  PomodoroStatus = 0
}

OnReloadTimer
{
  if PomodoroStatus > 0
  {
    '\C\1\c\_q\f[align,center]'
    --
    if WillResetPomodoro == 1
    {
      WillResetPomodoro = 0
      ResetPomodoroTimer(currentpomodoro)
    }
    else
    {
      OnShowTimer
    }
  }
}

//--------------------------------------------------
// 表示用関数
//--------------------------------------------------

OnTimerMenuSelected
{
  "\C\c/
  \_q\_l[0.5em,2em]◆ポモドーロタイマー/
  \_l[1.5em,@1.7em]\![*]\q[2-1,OnPomodoroSet,2,1]/
  \_l[1.5em,@1.7em]\![*]\q[10-2,OnPomodoroSet,10,2]/
  \_l[1.5em,@1.2em]\![*]\q[15-3,OnPomodoroSet,15,3]/
  \_l[1.5em,@1.2em]\![*]\q[25-5,OnPomodoroSet,20,4]/
  \_l[1.5em,@1.2em]\![*]\q[25-5,OnPomodoroSet,25,5]/
  /
  \_l[11.5em,2em]◆ループタイマー/
  \_l[12.5em,@1.7em]\![*]\q[1,OnLoopSet,1]/
  \_l[12.5em,@1.2em]\![*]\q[10,OnLoopSet,10]/
  \_l[12.5em,@1.2em]\![*]\q[15,OnLoopSet,15]/
  \_l[0.7em,13.25em]\__q[OnMenuClose]%(ICON('cross'))\__q\_l[@0.7em,]\__q[OnOpenMenu]%(ICON('arrow-left'))\__q/
  "
}

// 「BalloonTooltip_[選択肢ID]」にツールチップで表示させるテキストを記述
BalloonTooltip_OnPomodoroSet { "（作業時間）-(休憩時間)-（大休憩時間）" }

OnShowTimer
{
  _timerv = ''
  if (PomodoroStatus == 1) {
    _timerv = "\__q[OnPomodoroStop]%(ICON('stopwatch'))\f[height,12] %(PomodoroCount+1)セット目 %(TimerMin)/%(currentpomodoro)分\__q"
  } elseif (PomodoroStatus == 2) {
    _timerv = "\__q[OnTimerStop,%(LoopCount),%(_totalmin)]%(ICON('stopwatch'))\f[height,12] %(TimerMin)/%(CurrentLoop)分\__q"
  }
  if (PomodoroStatus > 0) {
    "\_l[0,13.3em]\0\f[align,right]\f[cursorstyle,none]\f[color,175,175,165]%(_timerv)\f[default]"
  } else {
    "\0\_l[@0.7em,]\__q[OnTimerMenuSelected]%(ICON('stopwatch'))\__q"
  }
}

