//******************************************************************************
// YAYA　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

OnFirstBoot
{
  OnBoot
}

//ゴーストのロード
//起動毎の変数の初期化などはここで
OnGhostLoad
{
  InitializeVariable
}

//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************
OnBoot
{
  GetProperties
  --
  InitialScripts
  --
  SetBalloonSurfaceSakura
  --
  _text = ''
  if !isEvent1End {
    '\![raise,OnEvent1_1]'
    return
  }
  _text = Hello
  InsertLineFeed(_text)
}

Hello
{
  _timeslot = GetTimeSlot
  ShowTopic(_timeslot)
  --
  'h1111204'
  'h1111201'
  --
  'よく来てくれたわね、'
  'いらっしゃい、'

  if _timeslot == '朝' {
    'おはよう、'
  }
  elseif _timeslot == '昼' || _timeslot == '日中' {
    'こんにちは、'
  }
  elseif _timeslot == '夜' || _timeslot == '深夜' {
    'こんばんは、'
  }
  --
  username + '。\n'
}

//******************************************************************************
//終了 OnClose
//******************************************************************************

OnClose
{
  if !isEvent1End {
    '\-'
    return
  }

  SetBalloonSurfaceSakura
  --
  InsertLineFeed(Bye)
  --
  '\-'
}

Bye
{
  'h1111209'
  'h1111207'
  --
  'あなたに'
  --
  'すばらしき朝'
  '蜜のようなまどろみ'
  '暗くて静かな安らぎ'
  '良き終わり'
  '孤独と救い'
  --
  'がありますように。'
  --
  "\nh1111204またね、%(username)。\_w[1200]"
}
