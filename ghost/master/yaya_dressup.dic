OnDressupChanged
{
  case shell_name {
    when 'master' {
      if reference[2] == 1 {
        // 着せられたとき
        case reference[3] {
          when 'ボトムス' {
			currentBottoms = reference[1]
          }
        }
      } else
      {
        if TalkStatus == 'none' {
          // 脱がされたとき
          case reference[3] {
            when 'トップス' {
              'h241108\_w[1000]h211101\_w[200]h211204'
            }
            when 'ボトムス' {
              'h241108\_w[1000]h211101\_w[200]h211209'
            }
          }
        }
      }
    }
  }
}

OnNotifyDressupInfo
{
  dressupInfo_ary = reference
  dressup.setup
}

dressup.setup
{
  _mark = (',', ';')
  categories_info_ary = IARRAY()
  parts_info_ary = IARRAY()
  _part_categories = IARRAY()
  _body_info_temp = ''

  foreach dressupInfo_ary; _info {

    _part_info = SPLIT(_info, CHR(0x1))
    _part_category = _part_info[1]

    if ASEARCH(_part_category, _part_categories) == -1 {

      _header_info_temp = _part_category

      foreach ('multiple', 'mustselect'); _option {
        if ASEARCH(_option, _part_info) != -1 {
          _header_info_temp += _mark[0] + 1
        }
        else {
          _header_info_temp += _mark[0] + 0
        }
      }

      _part_categories = (_part_categories, _part_category)
      categories_info_ary = (categories_info_ary, _header_info_temp)
      if _body_info_temp != '' {
        parts_info_ary = (parts_info_ary, RE_REPLACE(_body_info_temp, _mark[1]+'$', ''))
        _body_info_temp = ''
      }
    }

    _is_default = 0
    if ASEARCH('default', _part_info) != -1 {
      _is_default = 1
    }

    _is_equiped = _part_info[ARRAYSIZE(_part_info) - 2]

    _body_info_temp += _part_info[2] + _mark[0] + _is_default + _mark[0] + _is_equiped + _mark[1]
  }

  parts_info_ary = (parts_info_ary, _body_info_temp)
}

On_dressup.exec
{
  dressup.exec(reference)
}

dressup.exec
{
  _category_index = 0
  if GETTYPE(_argv[0]) == 1 {
    _category_index = _argv[0]
  }

  _fsize_left = 13
  _left_panel = '\f[height,' + _fsize_left + ']'
  _max_width = 0

  for _i=0; _i<ARRAYSIZE(categories_info_ary); _i++ {
    _info = categories_info_ary[_i]
    _left_panel += '\_l[0,' + (_i*(_fsize_left+2)) + ']'
    _name = _info[0]
    if STRLEN(_name) > _max_width {
      _max_width = STRLEN(_name)
    }

    if _i != _category_index {
      _left_panel += '\q[' + _name + ',On_dressup.exec,' + _i + ']'
    }
    else {
      _left_panel += '\![*]' + _name
    }
  }

  _right_panel_x = 7
  _fsize_right = 14
  _right_panel = ''

  _parts_ary = SPLIT(parts_info_ary[_category_index], ';')

  for _i=0; _i<ARRAYSIZE(_parts_ary); _i++ {
    _info = _parts_ary[_i]
    _name = _info[0]
    _is_equiped = TOINT(_info[2])
    _right_panel += '\f[height,' + _fsize_left + ']\_l[' + (_max_width+1) + 'em,' + (_i*(_fsize_right+2)) + ']'
    _right_panel += '\f[height,' + _fsize_right + ']'

    if _is_equiped {
      _right_panel += '\![*]'
    }
    _right_panel += '\q[' + _name + ',On_dressup.dress,' + _category_index + ',' + _i + ',' + (!_is_equiped) + ']'
  }

  _fsize = 14
  _close_button = '\f[height,' + _fsize + ']' + '\_l[0,18em]\q[x,script:\e]'

  '\C\0\c\b[2]\_q' + _left_panel + _right_panel + _close_button
}

On_dressup.dress
{
  dressup.dress(reference)
}

dressup.dress
{
  _wait = 1

  _category_index = _argv[0]
  _parts_index = _argv[1]
  _equip_status = _argv[2]

  _category_name = categories_info_ary[_category_index][0] 
  _part_name = parts_info_ary[_category_index][_parts_index,';'][0]

  '\![bind,' + _category_name + ',' + _part_name + ']'
  --
  '\![timerraise,' + _wait + ',1,On_dressup.exec,' + _category_index + ']'
}
