//******************************************************************************
// ランダムトーク
//******************************************************************************

RandomTalk
{
  UpdateUserRating
  lastanchor = ''
  lastresponce = systemuptime
  total_talk_count++

  SetBalloonSurfaceSakura
  --
  _talk = roulette('talks')
  _rate = EVAL('roulette.talks.prevrate')
  lasttalk = InsertLineFeed(_talk)

  if isdebug {
    "\0\_q%(_rate)\_q\n"
  }
  --
  ShowUserEmotions + lasttalk
}

talks : array
{
  _max = 100
  SetBalloonSurfaceSakura('library')
  --
  if user_emotions[4] > 1 {
    '/
    h1111203\1ハイネは窓のほうを見やり、雲を眺めている。/
    h1111201今日はいい日ね。そう思わない？\n/
    h1111206私、こうやって雲が流れていくのを見ているのが好きなの。\n/
    h1111209変わらない日々から開放されて、自由になる瞬間。大切なことよ。\n/
    ' + UpdateUserEmotions('喜び', 1, '不安', -1)

    '/
    \1(落ち着かない)\_w[1000]/
    h1111204なにか不安なことでもあるのかしら？\n/
    h1111209大丈夫よ。あなたを害するものは、ここには何一つないわ。\n/
    h1111205万が一あったとしても、私がそれを必ず取り除いてあげる。\n/
    h1111209……ああ、それとも、あなたが恐れているのは、h1111205私かしら？\n/
    h1111209私はあなたを害したりしないわ。きっとね。/
    ' + UpdateUserEmotions('不安', -1)
  }
  elseif user_emotions[0] > 1 {
    '/
    h1111207なんだか嬉しそうね。いいことでもあったのかしら。/
    '
  }

  {
    // ユーザの感情によらない普通のトーク

    {
      '/
      h1111209外から来た人と深く付き合うのは、これが初めてではないの。\n/
      h1111204例えば、あなたの雇い主とも面識があるわ。ほら、あの眼鏡の神経質そうな人。\n/
      h1111209あの子のおかげで私の存在は公の目をまぬがれているようなものよ。\n/
      彼女には本当に感謝しているの。/
      '
      --
      '\_w[1000]\1\c\_q/
      \![*]\_a[Boss1]詳しく聞かせて\_a\n/
      '
    }

    '/
    h1111209霧がなければ生きられない。\n/
    霧があるから生きている。\n/
    私は霧に生かされている。\n/
    \n/
    h1111205ああ、厭だ。/
    ' + UpdateUserEmotions('嫌悪', 1, '悲哀', 1)

    '/
    h1111209あなたたちが歩いている姿を見ていると、なんだか不思議。\n/
    h1111204いつも何かをしていて、どこかへ向かっている。\n/
    h1111207羨ましいわ。私はここに繋ぎ留められて、見ていることしかできないから。/
    ' + UpdateUserEmotions('興味', 1, '悲哀', 1)

    '/
    h1113105\1ハイネは掌の上で小さな何かを弄んでいる。よく見ると、それは木彫りの小鳥のようだった。\n/
    h1113202これは、私にとって外の世界を思い出させてくれる、数少ないもののうちのひとつなの。\n/
    \n/
    h1113205あなたの知る通り、私はこの街でしか生きられない。\n/
    h1111209けれど、いつも考えるの。この小鳥のような翼が生えて、このしがらみを飛び越えて行けたら、って。/
    ' + UpdateUserEmotions('悲哀', 1)

    {
      '/
      h1113202\1ハイネは本を読みながら何かを口に運んでいる。/
      h1113201ん、ああ、これは「フィッシュボーン・チップス」よ。/
      h1111209子供のころ好きだったの。ぱりぱりしてて、塩味が効いてて美味しいのよ。\n/
      h1123305といってもこれは本物じゃなくて、霧で作ったニセモノ。/
      味のしない欠陥品だけれど、「食べる」を忘れないためには必要なことなの。/
      '
      --
      UpdateUserEmotions('興味', 1, '悲哀', 1)
      --
      '\_w[1000]\1\c\_q/
      \![*]\_a[FishBoneChips1]行儀が悪いよ\_a\n/
      \![*]\_a[FishBoneChips2]どうやって作っていたの？\_a\n/
      '
    }

    {
      '/
      h1111205\1ハイネはエンバーミングツールを並べている。/
      h1111201遺体の保存処理をするときに使う道具で、腐敗しやすい内臓を取り出したりするの。\n/
      \n/
      h1111202もしあなたがここで死んだら、これで丁寧に処置してあげる。\n/
      h1111207死んだ後の約束よ。h1111204素敵でしょう？/
      ' + UpdateUserEmotions('不快', 1, '不安', 1)
      --
      '\_w[1000]\1\c\_q/
      \![*]\_a[EmbalmingTool1]本気で言っているの？\_a\n/
      \![*]\_a[EmbalmingTool2]その道具、どうするの？\_a\n/
      '
    }

    '/
    h1111209霧の中では、天気はいつも同じように見えるわね。\n/
    h1111209晴れているのか、曇っているのか、分かりはしないけれど。\n/
    h1111206唯一、雨は分かるわ。だから私は雨が好きなの。\n/
    h1111201あなたの好きな天気は？/
    ' + UpdateUserEmotions('興味', 1)

    "/
    h1111209頭のおかしい女のもとに足しげく通うのは、頭のおかしい%(sex)。/
    h1111204気が合うわね？h1111207ふふ。/
    " + UpdateUserEmotions('興味', 1, '不快', 1)

    '/
    h1111209黒死病が蔓延していたとき、問題になっていたのがいわゆる「早すぎた埋葬」。\n/
    h1111205ある技師は生き埋めにされる恐怖から逃れるため、棺の内側から埋葬者が生きていることを知らせる仕組みがついた棺を開発したの。\n/
    h1111204彼、デモンストレーションのために自ら生き埋めになってみせたそうよ。\n/
    \n/
    h1211209支援者に強制でもされたのかしら。自分からそんな真似を？なんにせよ、h1211205狂気の沙汰ね。/
    ' + UpdateUserEmotions('不安', 1, '興味', 1)

    '/
    h1111204霧を固めれば簡単な道具くらいは作れるわ。h1111209でも、しばらくすると解けてしまうし、h1111206何より結局は霧だから。\n/
    h1121209刃物はなまくら、部品はがたがた、生物は、h1121205人形未満。\n/
    h1121205ちょっとした遊びに使うくらいがせいぜいね。/
    '

    '/
    h1111205ある本を最初に読んだときの感動と、何度も読み返して全て見知った後の倦み。どちらがその本の真の印象かしら？\n/
    h1111209答えは両方。印象とはその時々で変わるもの。h1111205心の形が一つに定まることなんて、稀だもの。/
    ' + UpdateUserEmotions('興味', 1)

    {
      '/
      h1111106\1彼女の背は高い。/
      '
      --
      if sex == '男性' {
        '\1自分と同じかわずかに低い程度で、'
      }
      else {
        '\1自分が背伸びしても届かない程度には高く、'
      }
      --
      '/
      ぴんと伸びた背筋と相まって存在感がある。/
      '
      --
      '/
      h1111101‥‥？h1111204どうしたの、そんなに見つめて。/
      '
    }
  }
}

//------------------------------------------------------------------------------
// アンカートーク
//------------------------------------------------------------------------------
Boss1
{
  '/
  h1111209廃墟とか、不思議な都市伝説とかがある場所を巡るのが彼女の趣味でね。\n/
  h1111205出会ったときも、あの子は友人と調査半分、遊び半分で来ていた。\n/
  \n/
  散策を始めてすぐ、あの子は霧の中で友達とはぐれた。……もちろん、それは私が仕向けたのだけど。\n/
  h1121209あのときは話し相手に飢えていたのよ。存在を明かしてもいいって思うくらいにね。\n/
  \n/
  h1111206そして、一人になった彼女と接触して、ちょうど今のあなたのようにお話をした。\n/
  ここから出られないって話をしたらね、あの子、h1131104「貴方を外に出せるかどうかは分からない。でも、この場所はその時が来るまで必ず守る」\_w[750]h1111209って。\n/
  h1111205真剣な目をしてたのよ。その時の彼女は学生だったし、こんな広い土地をどうこうするなんて、/
  とても現実的じゃないでしょう？一時の興奮で出た言葉だということは分かったし、/
  そのときは面白いと思いこそすれ、期待はしていなかったのよ。\n/
  \n/
  h1111209でも、あの子は本気だった。\n/
  h1111205数年後にまたやってきたと思ったら、どこかから資金援助の取り決めを取ってきていて。/
  h1111209「約束を守りに来たよ」って言われたときは驚いたわ。\n/
  \n/
  h1111209そのあとはとんとん拍子に話が進んで、ここは立ち入りが制限されるようになった。\n/
  あとは知ってのとおりよ。\n/
  \n/
  h1111205……本当に、どうしてこんなに尽くしてくれるのかしら。\n/
  h1111209彼女には、いつか恩を返したいと思っているわ。/
  '
}

FishBoneChips1
{
  // 行儀が悪いよ
  '/
  h1113207あはは！お母さんにもよく言われていたわ。\n/
  h1113304いいじゃない、誰の機嫌を損ねるでもなし。/
  あなたさえ目を瞑ってくれれば、咎める人はいないわ。\n/
  h1111201なんだったら、あなたもいかが？\n/
  h1111207なんてね、冗談よ。……h1121204そんなに怖い顔しないで。/
  '
}
FishBoneChips2
{
  // どうやって作っていたの？
  '/
  h1113201そう難しくはないわ。魚の骨をオーブンで焼いて、塩を振るだけ。\n/
  h1111209ただ、食べるときは喉に刺さらないようによく噛むこと。\n/
  '
  // もうすこし続きを
}

EmbalmingTool1
{
  // 本気で言っているの？
  '/
  h1111101？ええ、本気だけど。\n/
  h1111105……。\n/
  h1111205……死を恐れる人々にとって、死後の約束は大切なもの、って読んだから試してみたのだけれど。\n/
  h1121204もしかして私、何か間違っていたかしら？\n/
  h1121209他人の気持ちって難しいのね。/
  '
}

EmbalmingTool2
{
  // その道具、どうするの？
  '/
  h1111209どうもしないわ。\n/
  h1111202資料を真似て作ったみただけの、ほんの手すさびよ。\n/
  h1111205霧で作った偽物だから、すぐにほどけて消えるわ。h1111106/
  \1\n\n言い終えたとき、彼女はすでにそれらへの興味を失っているようだった。\n/
  ほどなくして、彼女の言葉通りに道具は霧に溶けて消えた。\n/
  '
}

OnKeyPress
{
  if IsKeyBlocked {
    return
  }

  case reference[0] {
    when 't' {
      SetBalloonSurfaceSakura
      --
      RandomTalk
    }
    when 'm' {
      SetBalloonSurfaceSakura
      --
      '\![raise,OnMouseDoubleClick,0,0,0,0,__SYSTEM_KEYDOWN_COL,0,mouse]'
    }
    when 'r' {
      '\![reload,shiori]'
      --
      OnBoot
    }
    when 'd' {
      if isdebug {
        'ONにしました。再度dキーでOFF'
      }
      else {
        'OFFにしました'
      }
    }
  }

  if isdebug && ISFUNC('OnDebugKeyPress') {
    OnDebugKeyPress(reference[0])
  }
}

//------------------------------------------------------------------------------
//OnMinuteChangeイベント
//------------------------------------------------------------------------------

OnMinuteChange
{
  if (systemuptime - lastresponce) >= (60 * uwanosora_minute) {
    // 最後のランダムトーク・触り反応から
    // %(uwanosora_minute)分が経ったとき,上の空になる
    'h1111105'
    'h1111106'
  }
}

OnHourTimeSignal
{
  if IsJihouActive == '有効' {
    "\1%(hour)時だ"
  }
}

//------------------------------------------------------------------------------
//OnSecondChangeイベント
//1 秒毎に実行される。ここではあまり重い処理を行わないこと
//このテンプレートでは、見切れ処理のみ行っている
//---------------------------------------------------------------------------
OnSecondChange
{
  now = (hour, minute, second)
  ghostuptime = systemuptime - ghostboottime
}

//------------------------------------------------------------------------------
//OnSurfaceRestoreイベント
//------------------------------------------------------------------------------
OnSurfaceRestore
{
  'h1111103\_w[2500]h1111109\_w[100]h1111101'
  'h1111101\_w[2500]h1111109\_w[100]h1111102\_w[2500]h1111105'
  'h1111109\_w[2500]h1111103'
  'h1111109\_w[2500]h1111105'
  --
  '\1\s[10000]\e'
}

OnBalloonBreak
{
}

OnBalloonClose
{
  OnBalloonBreak
}

//------------------------------------------------------------------------------
//OnTranslateイベント
//------------------------------------------------------------------------------

OnTranslate
{
  _text = reference[0]

  if willTranslate {
    _text = TextOnlyTranslator(_text, 'TextOnlyTranslatorFunc')
  }
  else {
    willTranslate = 1
  }

  _text
}

// TextOnlyTranslator, TextOnlyTranslatorFunc: 以下より
// http://emily.shillest.net/ayaya/?cmd=read&page=Tips%2FOnTranslate%E3%81%AE%E4%BD%BF%E3%81%84%E6%96%B9&word=OnTranslate
TextOnlyTranslator
{
  _string = RE_SPLIT(_argv[0], '\\(\\|q\[.*?\]\[.*?\]|[!&8cfijmpqsn]\[.*?\]|[-*+1014567bcehntuvxz]|_[ablmsuvw]\[.*?\]|__(t|[qw]\[.*?\])|_[!?+nqsV]|[sipw][0-9])')
  _n = ARRAYSIZE(_string)
  _tag = RE_GETSTR()
  _tr = ''
  _qs = 0

  for _i=0; _i<_n; _i++ {
    _tr += EVAL("%(_argv[1])(_string[_i],_qs)")
    _tr += _tag[_i]
    if '\_q' _in_ _tag[_i] {
      _qs = !_qs
    }
  }

  _tr
}

TextOnlyTranslatorFunc
{
  //_argv[0] = 置換対象テキスト
  //_argv[1] = クイックセクション内か否か

  _text = _argv[0]
  if !_argv[1] {

    //サーフェス入力の簡単化と関数埋め込み: h1111101のようにしてサーフェス表示&まばたき補完関数を埋め込む
    _text = SurfaceSnippet(_text)

    // ウェイトの挿入や表記の置換
    _words = ( /
      '…\n', '\![quicksection,1]…\![quicksection,0]\n\_w[1000]', /
      '…', '\![quicksection,1]…\_w[500]\![quicksection,0]', /
      '。(?!」)', '。\_w[1200]', /
      '、(?!」)', '、\_w[600]', /
      '！(?!」)', '！\_w[900]', /
      '？(?!」)', '？\_w[1200]', /
      '。」', '。」\_w[1200]', /
      '、」', '、」\_w[600]', /
      '！」', '！」\_w[900]', /
      '？」', '？」\_w[1200]', /
      '\n\n', '\n\_w[1000]\n', /
      'あなた', 'おまえ' /
    )
    for _i=0; _i<ARRAYSIZE(_words); _i += 2 {
      _text = RE_REPLACE(_text, _words[_i], _words[_i+1])
      //φでエスケープできる（里々と同じ記法）
      _text = RE_REPLACE(_text, 'φ'+_words[_i+1], _words[_i])
    }

    // \Cが文頭以外に含まれている場合、適用されるように加工
    if STRSTR(_text, '\C', 0) > 0 {
      _text = '\C' + _text
    }
  }

  // 末尾のウェイトを削除(statusの'talking'判定を見た目通りにするため)
  _text = RE_REPLACE(_text, '\\_w\[\d+\]$', '')

  _text
}
