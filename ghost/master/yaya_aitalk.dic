//******************************************************************************
// ランダムトーク
//******************************************************************************

RandomTalk
{
  UpdateUserRating
  lastanchor = ""
  lastresponce = systemuptime
  total_talk_count++

  SetBalloonSurfaceSakura
  --
  lasttalk = InsertLineFeed(roulette.exec('talks'))
  lasttalk
}

talks : array
{
	_max = 100

	if CalcTension < _max/5 {
		SetBalloonSurfaceSakura("library")
		--
		"/
		h1111101あなた。聞こえているのでしょう、あなた。/
		"

		"/
		h1121105いつまでこんなことを続ければいいの。/
		"

		"/
		h1111205こんな世界、壊れてしまえばいいの。/
		"

		"/
		h1111103変化のない毎日のなか、小さな問題を見つけて、解決して、その繰り返し。\n/
		h1121302この倦みきった箱庭の中では、h1121305それが全てよ。/
		"

		"/
		h1112105さむい……。\n/
		どれだけ暖炉に火をくべても寒いの。\n/
		肌が冷たくてぴりぴりする。……h1112109どうして？/
		"

		"/
		h1122102息が、うまく吸えないの。ずっと胸が詰まっているみたい。\n/
		h1122105何もしていないのにこれだもの。\n/
		h1122209……本当に、いやになるわ。/
		"

		"/
		h1111306この街からは出られないの。\n/
		この身体は、霧があるところにしか居られない。\n/
		h1121204ああ、無理やり霧の外に出たって無駄よ。\n/
		h1121309目の前が真っ暗になって、気づけばまた霧の中。\n/
		h1111405……はあ。h1111105/
		"
	}

	elseif CalcTension < _max/5*2 {
		SetBalloonSurfaceSakura("library")
		--
		"/
		h1111105食事を作っていると惨めになるわ。死にたいと言いながら生きるための作業をしている。h1121205この体にもはや食事は要らないのだから、なおさら。/
		"

		"/
		h1111104毎日の習慣をこなせば、いくらか正気が保てる。h1111105日常に散りばめた正気のかけらを集めているのでしょう。/
		"

		"/
		h1111105何かを為せたつもりになっていたの？\n/
		h1121305本当に必要なことは何もできていないくせに。/
		"
	}

	elseif CalcTension < _max/5*3 {
		SetBalloonSurfaceSakura()
		--
		"/
		h1111105蘇りの約束された死なんて、死ではないわ。\n/
		h1131104だってそうでしょう。h1131409そんなもの、ただの眠りと変わらないじゃない。\n/
		h1132105霧に蝕まれたこの体は、ただただ醜く停滞しているだけ。\n/
		……h1112105本当に、いやになるわ。/
		"

		"/
		h1111209デストルドーを肯定するために。h1111204あなたもそうなのでしょう？/
		"
	}

	else {
		SetBalloonSurfaceSakura('garden')
		--
		"/
		h1111201あなたが来てくれていなければ死んでいたかもしれないわ。h1111209いえ、死んではいるのだけど。ふふ。h1111204それでもこうして笑えているのだもの。/
		"

		"/
		h1111209頭のおかしい女のもとに足しげく通うのは、頭のおかしい%(sex)。h1111204気が合うわね？h1111207ふふ。/
		"

		"/
		h1111205\1ハイネは棒状の道具をカチャカチャと並べている。/
		\0h1111201ふふ、気になる？h1111205これはエンバーミングツール。\n/
		遺体の保存処理をするときに使う道具よ。\n/
		腐敗しやすい内臓は、これらを使って取り出されるの。\n/
		\n/
		h1111202もしあなたがここで死んだら、これで丁寧に処置してあげる。\n/
		h1111207死んだ後の約束よ。h1111204素敵でしょう？/
		"

		"/
		h1111209黒死病が蔓延していたとき、問題になっていたのがいわゆる「早すぎた埋葬」。\n/
		h1111205ある技師は生き埋めにされる恐怖から逃れるため、棺の内側から埋葬者が生きていることを知らせる仕組みがついた棺を開発したの。\n/
		h1111204彼、デモンストレーションのために自ら生き埋めになってみせたそうよ。\n/
		\n/
		h1211209自分からそんな真似を？それとも支援者に強制でもされたのかしら。なんにせよ、h1211205奇妙で素敵なお話ね。/
		"

		"/
		h1111204霧を固めれば簡単な道具くらいは作れるわ。h1111209でも、しばらくすると解けてしまうし、h1111206何より結局は霧だから。\n/
		h1121209刃物はなまくら、部品はがたがた、生物は、h1121205人形未満。\n/
		h1121205ちょっとした遊びに使うくらいがせいぜいね。/
		"
	}
}

//------------------------------------------------------------------------------
// アンカートーク
//------------------------------------------------------------------------------

OnKeyPress
{
  if isKeyBlocked {
    return
  }

  case reference[0] {
    when "t" {
      SetBalloonSurfaceSakura
      --
      RandomTalk
    }
    when "m" {
      SetBalloonSurfaceSakura
      --
      "\![raise,OnMouseDoubleClick,0,0,0,0,__SYSTEM_KEYDOWN_COL,0,mouse]"
    }
    when "r" {
      "\![reload,shiori]"
	  --
	  OnBoot
    }
    when "d" {
      isdebug = ! isdebug
      isValidLookAtResponse = isdebug
      "h1111209\1デバッグモードを"
      --
      if isdebug {
        "ONにしました。再度dキーでOFF"
      }
      else {
        "OFFにしました"
      }
    }
  }

  if isdebug && ISFUNC("OnDebugKeyPress") {
    OnDebugKeyPress(reference[0])
  }
}

//******************************************************************************
// 時報
//******************************************************************************

//------------------------------------------------------------------------------
//OnMinuteChangeイベント
//------------------------------------------------------------------------------

OnMinuteChange
{
  if minute == 0 && IsJihouActive == "有効" {
    "\1%(hour)時だ"
  }

  if (systemuptime - lastresponce) >= (60 * uwanosora_minute) {
    // 最後のランダムトーク・触り反応から
    // %(uwanosora_minute)分が経ったとき,上の空になる
    "h1111105"
    "h1111106"
  }
}


//------------------------------------------------------------------------------
//OnSecondChangeイベント
//1 秒毎に実行される。ここではあまり重い処理を行わないこと
//このテンプレートでは、見切れ処理のみ行っている
//---------------------------------------------------------------------------
OnSecondChange
{
  now = (hour, minute, second)
  ghostuptime = systemuptime - ghostboottime
  UpdatePomodoroTimer
}

//------------------------------------------------------------------------------
//OnSurfaceRestoreイベント
//------------------------------------------------------------------------------
OnSurfaceRestore
{
  "h1111103\_w[2500]h1111109\_w[100]h1111101"
  "h1111101\_w[2500]h1111109\_w[100]h1111102\_w[2500]h1111105"
  "h1111109\_w[2500]h1111103"
  "h1111109\_w[2500]h1111105"
  --
  "\1\s[10000]\e"
}

OnBalloonBreak
{
}

OnBalloonClose
{
  OnBalloonBreak
}

//******************************************************************************
// トランスレータ
//******************************************************************************

//------------------------------------------------------------------------------
//OnTranslateイベント
//------------------------------------------------------------------------------

OnTranslate
{
  _text = reference[0]

  if willTranslate {
    _text = TextOnlyTranslator(_text,"TextOnlyTranslatorFunc")
  } else {
	willTranslate = 1
  }

  _text
}

// TextOnlyTranslator, TextOnlyTranslatorFunc: 以下より
// http://emily.shillest.net/ayaya/?cmd=read&page=Tips%2FOnTranslate%E3%81%AE%E4%BD%BF%E3%81%84%E6%96%B9&word=OnTranslate
TextOnlyTranslator
{
  _string = RE_SPLIT(_argv[0],"\\(\\|q\[.*?\]\[.*?\]|[!&8cfijmpqsn]\[.*?\]|[-*+1014567bcehntuvxz]|_[ablmsuvw]\[.*?\]|__(t|[qw]\[.*?\])|_[!?+nqsV]|[sipw][0-9])")
  _n = ARRAYSIZE(_string)
  _tag = RE_GETSTR()
  _tr = ""
  _qs = 0

  for _i = 0 ; _i < _n ; _i++ {
    _tr += EVAL("%(_argv[1])(_string[_i],_qs)")
    _tr += _tag[_i]
    if "\_q" _in_ _tag[_i] {
      _qs = ! _qs
    }
  }

  _tr
}

TextOnlyTranslatorFunc
{
  //_argv[0] = 置換対象テキスト
  //_argv[1] = クイックセクション内か否か

  _text = _argv[0]
  if ! _argv[1] {

    //サーフェス入力の簡単化と関数埋め込み: h1111101のようにしてサーフェス表示&まばたき補完関数を埋め込む
    _text = SurfaceSnippet(_text)

    // ウェイトの挿入や表記の置換
    _words = (/
        "…\n"     ,   "\![quicksection,1]…\![quicksection,0]\n\_w[1000]", /
        "…"       ,   "\![quicksection,1]…\_w[500]\![quicksection,0]",    /
        "。"      ,   "。\_w[1200]",                                        /
        "、"      ,   "、\_w[600]",                                         /
        "！"      ,   "！\_w[900]",                                         /
        "かしら？",   "かしら　\_w[1200]",                                  /
        "？"      ,   "？\_w[1200]",                                        /
        "\n\n"    ,   "\n\_w[1000]\n"                                       /
        )
    for _i = 0; _i < ARRAYSIZE(_words); _i+=2 {
      _text = REPLACE(_text,_words[_i],_words[_i + 1])
      //φでエスケープできる（里々と同じ記法）
      _text = REPLACE(_text,"φ"+_words[_i + 1],_words[_i])
    }

    // \Cが文頭以外に含まれている場合、適用されるように加工
    if STRSTR(_text, "\C", 0) > 0 {
      _text = "\C" + _text
    }

  }
  _text
}

