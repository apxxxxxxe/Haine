//******************************************************************************
// ランダムトーク
//******************************************************************************

RandomTalk
{
  UpdateUserRating
  lastanchor = ""
  lastresponce = systemuptime
  TalkStatus = "talking"
  total_talk_count++

  SetBalloonSurfaceSakura
  --
  lasttalk = InsertLineFeed(roulette.exec('talks'))
  lasttalk
  --
  "\![raise,OnRandomTalkEnd]"
}

OnRandomTalkEnd
{
  TalkStatus = "none"
}

talks : array
{
  SetBalloonSurfaceSakura + talk_all
}

talk_all : array
{
	{
		SetBalloonSurfaceSakura("library")
		--
		"h111101あなた。聞こえているのでしょう、あなた。"
		"h111105終わっても終わっても届かないのよ。いつまでも……いつまでもいつまでも！"
		"h121105いつまでこんなことを続ければいいの。"
	}

	{
		SetBalloonSurfaceSakura("library")
		--
		"h111105こうして肉を焼いていると惨めになるわ。死にたいと言いながら生きるための作業をしている。h121205この体にもはや食事は要らないのだから、なおさら。"
		"h111104毎日の習慣をこなせば、いくらか正気が保てるのでしょう。h111105日常に散りばめた正気のかけらを集めているの。"
	}

	{
		SetBalloonSurfaceSakura()
		--
		"/
		h111209デストルドーを肯定するために。h111204あなたもそうなのでしょう？/
		"

		"/
		h111205\1ハイネは棒状の道具をカチャカチャと並べている。/
		\0h111201ふふ、気になる？h111205これはエンバーミングツール。\n/
		遺体の保存処理をするときに使う道具よ。\n/
		腐敗しやすい内臓は、これらを使って取り出されるの。\n/
		もしあなたがここで死んだら、h111204これで丁寧に処置してあげる。\n/
		h111207嬉しいでしょう？/
		"
	}

	{
		SetBalloonSurfaceSakura('garden')
		--
		"h111201あなたが来てくれていなければ死んでいたかもしれないわ。h111209いえ、死んではいるのだけど。ふふ。h111204それでもこうして笑えているのだもの。"

		"h111209頭のおかしい女のもとに足しげく通うのは、頭のおかしい%(sex)。h111204気が合うわね？h111207ふふ。"
	}
}

//------------------------------------------------------------------------------
// アンカートーク
//------------------------------------------------------------------------------

OnKeyPress
{
  if isKeyBlocked {
    return
  }

  case reference[0] {
    when "t" {
      SetBalloonSurfaceSakura
      --
      RandomTalk
    }
    when "m" {
      SetBalloonSurfaceSakura
      --
      "\![raise,OnMouseDoubleClick,0,0,0,0,__SYSTEM_KEYDOWN_COL,0,mouse]"
    }
    when "r" {
      SetBalloonSurfaceSakura
      --
      "\1\![reload,shiori]shioriをリロードしました"
    }
    when "d" {
      isdebug = ! isdebug
      isValidLookAtResponse = isdebug
      "h111209\1デバッグモードを"
      --
      if isdebug {
        "ONにしました。再度dキーでOFF"
      }
      else {
        "OFFにしました"
      }
    }
  }

  if isdebug && ISFUNC("OnDebugKeyPress") {
    OnDebugKeyPress(reference[0])
  }
}

//******************************************************************************
// 時報
//******************************************************************************

//------------------------------------------------------------------------------
//OnMinuteChangeイベント
//------------------------------------------------------------------------------

OnMinuteChange
{
  if minute == 0 && IsJihouActive == "有効" {
    "\1%(hour)時だ"
  }

  if (systemuptime - lastresponce) >= (60 * uwanosora_minute) {
    // 最後のランダムトーク・触り反応から
    // %(uwanosora_minute)分が経ったとき,上の空になる
    "h111105"
    "h111106"
  }
}


//------------------------------------------------------------------------------
//OnSecondChangeイベント
//1 秒毎に実行される。ここではあまり重い処理を行わないこと
//このテンプレートでは、見切れ処理のみ行っている
//---------------------------------------------------------------------------
OnSecondChange
{
  now = (hour, minute, second)
  ghostuptime = systemuptime - ghostboottime
  UpdatePomodoroTimer
}

//------------------------------------------------------------------------------
//OnSurfaceRestoreイベント
//------------------------------------------------------------------------------
OnSurfaceRestore
{
  "h111103\_w[2500]h111109\_w[100]h111101"
  "h111103\_w[2500]h111109\_w[100]h111101"
  "h111101\_w[2500]h111109\_w[100]h111102\_w[2500]h111105"
  "h111101\_w[2500]h111109\_w[100]h111102\_w[2500]h111105"
  "h111109\_w[2500]h111103"
  "h111109\_w[2500]h111103"
  "h111109\_w[2500]h111105"
  "h111109\_w[2500]h111105"
  --
  "\1\s[10000]\e"
}

OnBalloonBreak
{
  TalkStatus = "none"
}

OnBalloonClose
{
  OnBalloonBreak
}

//******************************************************************************
// トランスレータ
//******************************************************************************

//------------------------------------------------------------------------------
//OnTranslateイベント
//------------------------------------------------------------------------------

OnTranslate
{
  _text = reference[0]

  if willTranslate {
    _text = TextOnlyTranslator(_text,"TextOnlyTranslatorFunc")
  } else {
	willTranslate = 1
  }

  _text
}

// TextOnlyTranslator, TextOnlyTranslatorFunc: 以下より
// http://emily.shillest.net/ayaya/?cmd=read&page=Tips%2FOnTranslate%E3%81%AE%E4%BD%BF%E3%81%84%E6%96%B9&word=OnTranslate
TextOnlyTranslator
{
  _string = RE_SPLIT(_argv[0],"\\(\\|q\[.*?\]\[.*?\]|[!&8cfijmpqsn]\[.*?\]|[-*+014567bcehntuvxz]|_[ablmsuvw]\[.*?\]|__(t|[qw]\[.*?\])|_[!?+nqsV]|[sipw][0-9])")
  _n = ARRAYSIZE(_string)
  _tag = RE_GETSTR()
  _tr = ""
  _qs = 0

  for _i = 0 ; _i < _n ; _i++ {
    _tr += EVAL("%(_argv[1])(_string[_i],_qs)")
    _tr += _tag[_i]
    if "\_q" _in_ _tag[_i] {
      _qs = ! _qs
    }
  }

  _tr
}

TextOnlyTranslatorFunc
{
  //_argv[0] = 置換対象テキスト
  //_argv[1] = クイックセクション内か否か

  _text = _argv[0]
  if ! _argv[1] {

    //サーフェス入力の簡単化と関数埋め込み: h111101のようにしてサーフェス表示&まばたき補完関数を埋め込む
    _text = SurfaceSnippet(_text)

    // ウェイトの挿入や表記の置換
    _words = (/
        "…\n"     ,   "\![quicksection,1]…\![quicksection,0]\n\_w[1000]", /
        "…"       ,   "\![quicksection,1]…\_w[500]\![quicksection,0]",    /
        "。"      ,   "。\_w[1200]",                                        /
        "、"      ,   "、\_w[600]",                                         /
        "！"      ,   "！\_w[900]",                                         /
        "かしら？",   "かしら　\_w[1200]",                                  /
        "？"      ,   "？\_w[1200]",                                        /
        "\n\n"    ,   "\n\_w[1000]\n"                                       /
        )
    for _i = 0; _i < ARRAYSIZE(_words); _i+=2 {
      _text = REPLACE(_text,_words[_i],_words[_i + 1])
      //φでエスケープできる（里々と同じ記法）
      _text = REPLACE(_text,"φ"+_words[_i + 1],_words[_i])
    }

    // \Cが文頭以外に含まれている場合、適用されるように加工
    if STRSTR(_text, "\C", 0) > 0 {
      _text = "\C" + _text
    }

  }
  _text
}

