//******************************************************************************
// YAYA　ゴーストテンプレート
// メニュー処理辞書
//******************************************************************************

//------------------------------------------------------------------------------
//メインメニュー展開
//------------------------------------------------------------------------------
OnOpenMenu
{
  _quickdialogue = ''
  if TalkStatus == 2
  {
    _quickdialogue = '\_q'
    '\C\c'
  }
  --

  if (メニュー台詞 == '')
  {
    メニュー台詞 = メニュー一言
  }

  if GETTYPE(_argv[0]) == 3 && _argv[0] != ""
  {
    メニュー台詞 = _argv[0]
  }

  TalkStatus = 2
  --
  "%(メインメニュー)%(_quickdialogue)%(メニュー台詞)"
}

メインメニュー
{
  "\![set,autoscroll,disable]\_q\*\0/
  //\_l[0,1.2em]------------------------------------/
  \_l[0,1.2em]/
  \_l[0.8em,@2.0em]\![*]\q[話して,Menu_RandomTalk]/
  \_l[0.8em,@1.3em]\![*]\q[さっきの話,Menu_LastTalk]/
  \_l[0.8em,@1.3em]\![*]\q[Web拍手,OnWebClap]/
  \_l[0.7em,13.25em]\__q[OnMenuClose]%(ICON('cross'))\__q/
  \_l[@0.7em,]\__q[OnConfigMenu]%(ICON('cog'))\__q"
  --
  if ISFUNC(OnShowTimer) {OnShowTimer}
  --
  "\0\_l[0,0]\_q"
}

メニュー一言{
  if (GetSurfaceElements(5) == 6 || GetSurfaceElements(5) == 3) {
    "……h11" + GetSurfaceElements(3) + "10" + EVAL(GetSurfaceElements(5) - 2) + "\w4h11" + GetSurfaceElements(3) + "201あ、呼んだかしら？"
  } elseif (lastmouseevent == 'MouseDoubleClick0bust') {
    "h112207ヘンタイ！"
    "h112204楽しいの？"
  } else {
    if (systemuptime - lastresponce) >= (60 * uwanosora_minute) {
      // 最後のランダムトーク・触り反応から%(uwanosora_minute)分が経ったとき
      "……h114201ああ、h114204何かしら。"
    }
    "h111204はいφ、なにφ？"
    "h111201うんφ、どうしたのφ？"
    "h114201聞くわ。"
    "h114101どうしたのφ？"
  }
}

OnConfigMenu
{
  TalkStatus = 2
  '\C\c'
  --
  "%(設定メニュー)\e"
}

設定メニュー
{
  _talkintervalmin = aitalkinterval / 60
    _talkintervalmessage = "なし"
    if _talkintervalmin != 0 {
      _talkintervalmessage = "%(_talkintervalmin)分"
    }

  "\b[4]\_q/
    \_l[0.5em,2em]◆喋り頻度\_l[@0.5em,]【現在\_l[@0.3em,]%(_talkintervalmessage)】/
    \_l[2em,@1.5em]\q[1,OnTalkIntervalSet,60]\_l[@0.5em,]\q[3,OnTalkIntervalSet,180]\_l[@0.5em,]\q[5,OnTalkIntervalSet,300]\_l[@0.5em,]\q[10,OnTalkIntervalSet,600]\_l[@0.5em,]\q[なし,OnTalkIntervalSet,0]/
    \_l[0.5em,@2em]◆時報\_l[@0.5em,]【現在\_l[@0.3em,]%(IsJihouActive)】/
    \_l[2em,@1.5em]\q[有効,OnJihouSelected,1]\_l[@0.5em,]\q[無効,OnJihouSelected,0]/
    \_l[0.7em,13.25em]\__q[OnMenuClose]%(ICON('cross'))\__q\_l[@0.7em,]\__q[OnOpenMenu]%(ICON('arrow-left'))\__q/
    "
}

OnJihouSelected
{
  case reference[0]
  {
    when 0
    {
      IsJihouActive = '無効'
    }
    when 1
    {
      IsJihouActive = '有効'
    }
  }
  設定メニュー
}

OnTalkIntervalSet
{
  aitalkinterval = reference[0]
    /*
       _message = ""
       case aitalkinterval {
       when 600 {
       _message = "h111209少しφ、\_w[250]作業のほうに集中するわね"
       }
       when 300 {
       _message = "h111209ゆっくり話しましょう"
       }
       when 180 {
       _message = "h111204いつもどおり話すわ"
       }
       when 60 {
       _message = "h111201たくさんφ？\_w[500]　h111207がんばってみるわ"
       }
       when 0 {
       _message = "h111209静かにしているわ"
       }
       others {
       "\1デバッグ：喋り頻度に意図しない値が入力されました"
       }
       }
       */
    "\C\c"
    --
    OnConfigMenu
}

//------------------------------------------------------------------------------
//メニュー選択タイムアウト
//------------------------------------------------------------------------------
OnChoiceTimeout
{
}

//******************************************************************************
//各項目の処理内容
//******************************************************************************

OnMenuClose
{
  メニュー台詞 = ''
  TalkStatus = 0
  if ISFUNC(OnReloadTimer) {"\![timerraise,0,0,OnReloadTimer]"} // 予約を削除
}
//アンカー　ツールチップ

//---- 何か話して
Menu_RandomTalk
{
  TalkStatus = 1
  "h114201いいわよ。h114205……そうね、\n\c"
  "h113309なんの話がいいかしら……h113205そうね、\n\c"
  --
  RandomTalk
  --
  '\![raise,OnRandomTalkExEnd]'
}

//---- さっきの話は
Menu_LastTalk
{
  lastanchor = ""
  "h111209さっきの話ね\_q\n\n\_l[0,@%(balloon.lines+1)em]　\_l[0,@-%(balloon.lines+1)em]\_q"//\w6\n\_q-\w1---\w1------\w1--------------\w1-------\w1----\w1---\w1--\w1-\n\_w[500]\_l[0,@%(balloon.lines)em]　\_l[0,@-%(balloon.lines)em]\_q"
  --
  lasttalk
}

