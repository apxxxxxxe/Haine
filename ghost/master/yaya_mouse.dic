//******************************************************************************
// YAYA　ゴーストテンプレート
// マウス入力関連辞書
//******************************************************************************


//******************************************************************************
//マウス処理部
//
//以下、実際のトークを記述します。関数名は以下の規則になっています。
//[種別] + [スコープ] + [部位]
//種別は、MouseMove, MouseDoubleClick, MouseWheelUp, MouseWheelDown のどれかです。
//スコープは、\0,\1,といったスコープ番号です。
//部位は、当たり判定のどの部位かを表します。当たり判定の無い場所でのイベントの場合
//部位はありません。
//******************************************************************************


//******************************************************************************
//撫で反応
//******************************************************************************

MouseMove0head : nonoverlap
{
  good_touch_count++
  "h1111105……あたたかいのね、あなたの手。"
  "h1111205……軽んじられているような気もするわ。h1111209まあ、いいけれど。"
}

MouseMove0face : nonoverlap
{
  "h1121207くすぐったい。"
}

MouseMove0bust : nonoverlap
{
  // 怒られるまでの回数
  _threshold = 7

  bad_touch_count++
  if touch_count < _threshold {
    "h1111201ずいぶん必死ね。h1111304楽しいの？"
    "h1111201……外ではこれが普通なの？h1111204違うわよね。"
    "h1111202……いくら他人の目がないとはいえ、h1111205品性を疑うわ。"
	"h1111304面白い顔。h1111309鏡で見せてあげたいわ。"
  } elseif touch_count == _threshold {
	"h1111205\1触れようとした手先が、霧に溶けた。\n慌てて引っ込めると、手は元通りになった。h1111201許されていると思ったの？\nh1111204わきまえなさい。"
  } else {
	"h1111204\1自重しよう……。"
  }
}

//******************************************************************************
//つつかれ反応
//******************************************************************************

MouseDoubleClick0
{
  OnOpenMenu
}

//******************************************************************************
// めくり反応（上ホイール）
//******************************************************************************
MouseWheelUp0skirt : nonoverlap
{
	// 呆れ/怒られるまでの回数
	_threshold = 3

	_validBottoms = ("フレア黒", "フレア長黒")
	if ASEARCH(currentBottoms, _validBottoms) == -1 {
	  // めくり非対応のボトムス
	  "\1(このボトムスはめくれないようだ)"
	} else {
	  "\t\0"
	  --
	  if touch_count < _threshold {
		// 赤面
		"h2244402……！\n"
	  } else {
		// 真顔
		"h2114105……\n"
	  }
	  --
	  if touch_count < _threshold {
		"h1241102\_w[500]"
		--
		"h1131404あなたねえ。\nh1131104やっていいことと悪いことがあるわよ。"
		"h1111301\_w[100]h1111304やってくれたわね。"
		"h1111301\_w[100]h1111304覚えておきなさい。"
	  } else {
		"h1111101\_w[100]h1111104悪戯がすぎるわよ。"
		"h1111103……h1111106何が楽しいんだか。"
		"h1111309……屈辱というやつね。h1111204いつか必ず返すわ。"
	  }
	}
}

//******************************************************************************
// 引き寄せ反応（下ホイール）
//******************************************************************************

MouseWheelDown0shoulder
{
  if touch_count == 1 {
    "h1111209ん。\nh1111205……。\nh1111201………h1111204ふふ。"
  }
  elseif touch_count == 2 {
    "h1000000あっ……。h1111209…………h1111205まだ、こうしていたいの？"
  }
  elseif touch_count > 2 {
    "h1111209……。"
  }
}

MouseWheelDown0hand
{
  if touch_count == 1 {
    "h1111202うん？\n……h1111205手を引いて、h1111204それからどうするの？"
    "h1111204……どこかへ連れて行ってくれるの？\n1111209残念だけど、私はここを動かないわよ。"
  }
  elseif touch_count == 2 {
    "h1111205ぎゅーっ……h1111201。h1111204ふふ。"
    "h1111209……離してくれないの？困ったわね……h1111207ふふ……"
  }
  elseif touch_count > 2 {
    "……。"
  }
}

DialogSurprised
{
  "！……"
  "なっ……"
}

DialogConfused
{
  "……"
  "あの……"
}

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************

// 共通処理
OnMouseEvent
{
  TalkStatus = "touch"
  lastresponce = systemuptime
  first_touch = 1

  "\![timerraise,30000,1,OnTouchCountReset,reset]"
  --
  SetBalloonSurfaceSakura
  --
  InsertLineFeed(NextTouchTalk)
  --
  "\![raise,OnAfterMouseEvent]"
}

OnAfterMouseEvent
{
  TalkStatus = 'none'
}

OnTouchCountReset
{
  touch_count = 0
  last_touch_part = ""
}

OnMouseMove
{
  AYATEMPLATE.MouseMoveWheelFunc(0,50,"MouseMove")
}

OnMouseDoubleClick
{
  NextTouchTalk = AYATEMPLATE.MouseEventExec("MouseDoubleClick")
  if NextTouchTalk != "" {
    "\![raise,OnMouseEvent]"
  }
}

OnMouseClickEx
{
  if reference[5] == "middle" {
    NextTouchTalk = AYATEMPLATE.MouseEventExec("MouseWheelClick")
    if NextTouchTalk != "" {
      "\![raise,OnMouseEvent]"
    }
  }
}

OnMouseWheel
{
	_result = ''
	if TOINT(reference[2]) > 0 {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelUp')
	}
	else {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelDown')
	}
	
	if _result == '' {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheel')
	}
	
	_result
}

IsMouseTalkExist
{
  // _argv[0]: OnMouseClickなど
  // _argv[1]: reference[3]
  // _argv[2]: reference[4]
  _fname1 = _argv[0] + TOSTR(_argv[1]) + TOSTR(_argv[2])
  _fname2 = _argv[0] + TOSTR(_argv[1])

  if ISFUNC(_fname1) || ISFUNC(_fname2) {
    1
  }
  else {
    0
  }
}

//******************************************************************************
//マウスイベントコア
//******************************************************************************

TouchCount
{
  // _argv[0]: 触り反応の部位
  if last_touch_part == _argv[0] {
    touch_count += 1
  }
  else {
    touch_count = 1
  }
  last_touch_part = _argv[0]
}

AYATEMPLATE.MouseEventExec
{
  _fname = _argv[0] + TOSTR(reference[3]) + TOSTR(reference[4])
  if ISFUNC(_fname) {
    TouchCount(reference[4]) // 追加
    EVAL(_fname)
    return
  }

  _fname = _argv[0] + TOSTR(reference[3])
  if ISFUNC(_fname) {
    TouchCount(reference[4]) // 追加
    EVAL(_fname)
    return
  }
}

AYATEMPLATE.MouseMoveWheelFunc
{
  _side = TOINT(reference[3])
  if _argv[0] {
    _side += 10
  }

  if AYATEMPLATE.strokeid[_side] == reference[4] {
    if (GETTICKCOUNT - TOINT(AYATEMPLATE.stroketimer[_side])) > 1500 {
      AYATEMPLATE.stroke[_side] = 1
    }
    AYATEMPLATE.stroketimer[_side] = GETTICKCOUNT

    _stroke = TOINT(AYATEMPLATE.stroke[_side])
    _stroke += 1
    AYATEMPLATE.stroke[_side] = _stroke

    if _stroke >= _argv[1] {
      if 'talking' !_in_ status {
        AYATEMPLATE.stroke[_side] = 1
        // ↓追加・変更
        NextTouchTalk = AYATEMPLATE.MouseEventExec(_argv[2]);
        if NextTouchTalk != "" {
          "\![raise,OnMouseEvent]"
        }
        // ↑追加・変更ここまで

      }
    }
  }
  else {
    AYATEMPLATE.stroke[_side] = 1
    AYATEMPLATE.stroketimer[_side] = GETTICKCOUNT
    AYATEMPLATE.strokeid[_side] = reference[4]
  }
}

