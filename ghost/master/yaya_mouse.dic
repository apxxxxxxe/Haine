//******************************************************************************
// YAYA　ゴーストテンプレート
// マウス入力関連辞書
//******************************************************************************


//******************************************************************************
//マウス処理部
//
//以下、実際のトークを記述します。関数名は以下の規則になっています。
//[種別] + [スコープ] + [部位]
//種別は、MouseMove, MouseDoubleClick, MouseWheelUp, MouseWheelDown のどれかです。
//スコープは、\0,\1,といったスコープ番号です。
//部位は、当たり判定のどの部位かを表します。当たり判定の無い場所でのイベントの場合
//部位はありません。
//******************************************************************************


//******************************************************************************
//撫で反応
//******************************************************************************

MouseMove0head : nonoverlap
{
  good_touch_count++
  "h111105……あたたかいのね、あなたの手。"
  "h111205……軽んじられているような気もするわ。h111209まあ、いいけれど。"
}

MouseMove0face : nonoverlap
{
  "h121207くすぐったい。"
}

MouseMove0bust : nonoverlap
{
  bad_touch_count++
  "h111204……外ではこれが普通なの？h111207違うわよね。"
  "h111202……いくら他人の目がないとはいえ、h111205品性を疑うわ。"
  "h111205\1触れようとした手先が、霧に溶けた。\n慌てて引っ込めると、手は元通りになった。h111201許されると思ったの？h111204わきまえなさい。"
}

//******************************************************************************
//つつかれ反応
//******************************************************************************

MouseDoubleClick0
{
  OnOpenMenu
}

//******************************************************************************
// めくり反応（上ホイール）
//******************************************************************************
MouseDoubleClick0skirt
{
	if !ISVAR("currentBottoms") {
	  "\1(めくり反応を正しく動かすためには初期設定が必要です。\n着せ替えメニューから最低１回ボトムスを着せ替えてください。)"
	} else {
	  "\![bind,ボトムス,%(currentBottoms)めくれ,1]"
	  --
	  "h214402……！\n"
	  --
	  "\![bind,ボトムス,%(currentBottoms),1]"
	  --
	  "h111104……覚えておきなさい。"
	  "h111204……悪戯がすぎるわよ。"
	  "h111105……何が楽しいんだか。"
	}
}

//******************************************************************************
// 引き寄せ反応（下ホイール）
//******************************************************************************

MouseWheelDown0shoulder
{
  if touch_count == 1 {
    "h111209ん。\nh111205……。\nh111201………h111204ふふ。"
  }
  elseif touch_count == 2 {
    "h100000あっ……。h111209…………h111205まだ、こうしていたいの？"
  }
  elseif touch_count > 2 {
    "h111209……。"
  }
}

MouseWheelDown0hand
{
  if touch_count == 1 {
    "h111202うん？\n……h111205手を引いて、h111204それからどうするの？"
    "h111204……どこかへ連れて行ってくれるの？\n111209残念だけど、私はここを動かないわよ。"
  }
  elseif touch_count == 2 {
    "h111205ぎゅーっ……h111201。h111204ふふ。"
    "h111209……離してくれないの？困ったわね……h111207ふふ……"
  }
  elseif touch_count > 2 {
    "……。"
  }
}

//******************************************************************************
// 見つめ反応（静止時）
//******************************************************************************

OnMouseLookAtbust : nonoverlap
{
  bad_touch_count++
  "……h112204視線って案外わかるのよ？\nやるならh112207悟られないようにしないと。"
  "……h212109見すぎよ。"
}

OnMouseLookAtBeforebust : nonoverlap
{
  "h112104"
}

OnMouseLookAtface : nonoverlap
{
  "……h321207顔に何かついてる？"
  "h321201……h321207そんなに見つめられると、困ってしまうわ。\nh121204面白いわけでもないでしょう？"
}

OnMouseLookAtBeforeface : nonoverlap
{
  "h321201"
}

DialogSurprised
{
  "！……"
  "なっ……"
}

DialogConfused
{
  "……"
  "あの……"
}

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************

// 触り反応時の共通処理
CommonEventMousePre
{
  TalkStatus = "touch"
  lastresponce = systemuptime
  first_touch = 1
}

OnMouseEvent
{
  "\![timerraise,30000,1,OnTouchCountReset,reset]"
  --
  SetBalloonSurfaceSakura
  --
  CanselLookAt
  --
  InsertLineFeed(NextTouchTalk)
}

OnTouchCountReset
{
  touch_count = 0
  last_touch_part = ""
}

OnMouseMove
{
  AYATEMPLATE.MouseMoveWheelFunc(0,50,"MouseMove")
}

OnMouseDoubleClick
{
  NextTouchTalk = AYATEMPLATE.MouseEventExec("MouseDoubleClick")
  if NextTouchTalk != "" {
    CommonEventMousePre
    "\![raise,OnMouseEvent]"
  }
}

OnMouseClickEx
{
  if reference[5] == "middle" {
    NextTouchTalk = AYATEMPLATE.MouseEventExec("MouseWheelClick")
    if NextTouchTalk != "" {
      CommonEventMousePre
      "\![raise,OnMouseEvent]"
    }
  }
}

OnMouseWheel
{
	_result = ''
	if TOINT(reference[2]) > 0 {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelUp')
	}
	else {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelDown')
	}
	
	if _result == '' {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheel')
	}
	
	_result
}

CanselLookAt
{
  "\![timerraise,0,1,OnMouseLookAtBefore%(_argv[0])]"
  --
  "\![timerraise,0,1,OnMouseLookAt%(_argv[0])]"
}

OnMouseLookAtBefore
{
  "\C\![raise,OnMouseLookAtBefore%(reference[0])]"
}

IsMouseTalkExist
{
  // _argv[0]: OnMouseClickなど
  // _argv[1]: reference[3]
  // _argv[2]: reference[4]
  _fname1 = _argv[0] + TOSTR(_argv[1]) + TOSTR(_argv[2])
  _fname2 = _argv[0] + TOSTR(_argv[1])

  if ISFUNC(_fname1) || ISFUNC(_fname2) {
    1
  }
  else {
    0
  }
}

//******************************************************************************
//マウスイベントコア
//******************************************************************************

TouchCount
{
  // _argv[0]: 触り反応の部位
  if last_touch_part == _argv[0] {
    touch_count += 1
  }
  else {
    touch_count = 1
  }
  last_touch_part = _argv[0]
}

AYATEMPLATE.MouseEventExec
{
  _fname = _argv[0] + TOSTR(reference[3]) + TOSTR(reference[4])
  if ISFUNC(_fname) {
    TouchCount(reference[4]) // 追加
    EVAL(_fname)
    return
  }

  _fname = _argv[0] + TOSTR(reference[3])
  if ISFUNC(_fname) {
    TouchCount(reference[4]) // 追加
    EVAL(_fname)
    return
  }
}

AYATEMPLATE.MouseMoveWheelFunc
{
  _side = TOINT(reference[3])
  if _argv[0] {
    _side += 10
  }

  if AYATEMPLATE.strokeid[_side] == reference[4] {
    if (GETTICKCOUNT - TOINT(AYATEMPLATE.stroketimer[_side])) > 1500 {
      AYATEMPLATE.stroke[_side] = 1
    }
    AYATEMPLATE.stroketimer[_side] = GETTICKCOUNT

    _stroke = TOINT(AYATEMPLATE.stroke[_side])
    _stroke += 1
    AYATEMPLATE.stroke[_side] = _stroke

    if _stroke >= _argv[1] {
      if 'talking' !_in_ status {
        AYATEMPLATE.stroke[_side] = 1
        // ↓追加・変更
        NextTouchTalk = AYATEMPLATE.MouseEventExec(_argv[2]);
        if NextTouchTalk != "" {
          CommonEventMousePre
          "\![raise,OnMouseEvent]"
        }
        // ↑追加・変更ここまで

      }
    }
  }
  else {
    AYATEMPLATE.stroke[_side] = 1
    AYATEMPLATE.stroketimer[_side] = GETTICKCOUNT
    AYATEMPLATE.strokeid[_side] = reference[4]
  }
}

